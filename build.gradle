// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.major = 0
    ext.minor = 1
    ext.patch = 0

    def buildConfig         = [:]
    buildConfig.compileSdk  = 28
    buildConfig.targetSdk   = 28
    buildConfig.minSdk      = 14
    buildConfig.code        = (major * 1000000) + (minor * 10000) + (patch * 100)
    buildConfig.name        = "$major.$minor.$patch"
    ext.buildConfig         = buildConfig

    def publishConfig       = [:]
    publishConfig.userOrg   = 'raizlabs'
    publishConfig.repoName  = 'maven'
    publishConfig.groupId   = 'com.righpoint'
    publishConfig.website   = "https://github.com/Raizlabs/viewmodel-inject"
    ext.publishConfig       = publishConfig

    ext.versions = [
            'kotlin': '1.3.11',
            'dagger': '2.20'
    ]

    ext.deps = [
            'dagger': "com.google.dagger:dagger:${versions.dagger}",
            'daggerCompiler': "com.google.dagger:dagger-compiler:${versions.dagger}",
            'daggerAndroid': "com.google.dagger:dagger-android-support:${versions.dagger}",
            'daggerAndroidCompiler': "com.google.dagger:dagger-android-processor:${versions.dagger}",
            'autoService': 'com.google.auto.service:auto-service:1.0-rc4',
            'autoCommon': 'com.google.auto:auto-common:0.10',
            'junit': 'junit:junit:4.12',
            'truth': 'com.google.truth:truth:0.42',
            'mockito': 'org.mockito:mockito-core:2.23.4',
            'androidTruth': 'androidx.test.ext:truth:1.0.0',
            'kotlinJdk7': "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin}",
            'kotlinJdk8': "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${versions.kotlin}",
            'javaPoet': 'com.squareup:javapoet:1.11.1',
            'guava': 'com.google.guava:guava:27.0.1-jre',
            'compileTesting': 'com.google.testing.compile:compile-testing:0.15',
            'robolectric': 'org.robolectric:robolectric:4.0.2',
            'inject': 'javax.inject:javax.inject:1',
            'android': 'com.google.android:android:4.1.1.4',
            'androidxAnnotations': 'androidx.annotation:annotation:1.0.0',
            'appcompat': "androidx.appcompat:appcompat:1.0.2",
            'constraintLayout': "androidx.constraintlayout:constraintlayout:1.1.3",
            'fragment': 'androidx.fragment:fragment:1.0.0',
            'lifecycleExtensions': 'androidx.lifecycle:lifecycle-extensions:2.0.0',
            'viewModel': 'androidx.lifecycle:lifecycle-viewmodel:2.0.0',
            'dexMemberList': 'com.jakewharton.dex:dex-member-list:3.2.1',
            'testRunner': 'androidx.test:runner:1.1.0',
            'testRules': 'androidx.test:rules:1.1.0',
            'espresso': 'androidx.test.espresso:espresso-core:3.1.0',
            'espressoContrib': 'androidx.test.espresso:espresso-contrib:3.1.0'
    ]

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath 'com.android.tools.build:gradle:3.3.0-rc02'
        classpath 'com.novoda:bintray-release:0.9'
    }

    repositories {
        mavenCentral()
        google()
        jcenter()
    }
}

subprojects {
    repositories {
        mavenCentral()
        google()
        jcenter()
    }

    // Guava is depended on transitively. Force it to the same version to prevent missing methods.
    // This occurs when the compilation classpath picks 23.x whereas tests need 25.x.
    configurations.all {
        it.resolutionStrategy {
            it.force(deps.guava)
        }
    }

//    group = GROUP
//    version = VERSION_NAME
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
